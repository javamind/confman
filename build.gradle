import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'org.postgresql:postgresql:9.3-1102-jdbc41'
        classpath 'org.flywaydb:flyway-gradle-plugin:3.0'
        //classpath 'com.oracle:ojdbc6:11.2.0.4'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'flyway'
apply plugin: 'maven'

group = 'com.ninjamind'
version = '1.0.0-SNAPSHOT'

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'http://repo.spring.io/plugins-release'
    }
}

idea {
    module {
        inheritOutputDirs = true
    }
}

dependencies {
    compile("com.google.guava:guava:16.0.1")
    //compile("com.oracle:ojdbc6:11.2.0.4")
    compile("org.postgresql:postgresql:9.3-1102-jdbc41")
    compile("com.github.zafarkhaja:java-semver:0.7.2")
    compile("org.springframework.boot:spring-boot-starter-web:1.2.0.M1")
    compile("org.springframework.boot:spring-boot-starter-data-jpa:1.2.0.M1")

    testCompile("org.springframework.boot:spring-boot-starter-test:1.2.0.M1")
    testCompile("com.ninja-squad:DbSetup:1.3.0")
    testCompile("org.assertj:assertj-core:1.5.0")
    testCompile("org.mockito:mockito-all:1.8.5")
    testCompile("junit:junit:4.11")
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}
//You can change the default database which is postgreSQL
String myDataBase = "postgresql"
if (project.properties.containsKey("database")) {
    myDataBase = project.property("database")
}
processResources {
    println "The database used is " + myDataBase
    filter ReplaceTokens, tokens: [
            "db.driver"         : project.property("db." + myDataBase + ".driver"),
            "db.url"            : project.property("db." + myDataBase + ".url"),
            "db.username"       : project.property("db." + myDataBase + ".username"),
            "db.password"       : project.property("db." + myDataBase + ".password"),
            "db.supressclose"   : project.property("db." + myDataBase + ".supressclose"),
            "hibernate.dialect" : project.property("db." + myDataBase + ".hibernate.dialect"),
            "hibernate.show_sql": project.property("db." + myDataBase + ".hibernate.show_sql"),
    ]
}

//flyway
project.ext['flyway.user'] = project.property("db." + myDataBase + ".username")
project.ext['flyway.password'] = project.property("db." + myDataBase + ".password")
if (project.properties.containsKey("flyway.schemas") && !project.properties.containsKey("flyway.schemas").equals("")) {
    project.ext['flyway.schemas'] = project.property("db." + myDataBase + ".schemas")
}
project.ext['flyway.url'] = project.property("db." + myDataBase + ".url")
project.ext['flyway.locations'] = "db.migration." + myDataBase

//Artifact and release build.
//We have to define classpath
jar {
    baseName = 'confman'
    manifest {
        attributes(
                "Manifest-Version": "1.0",
                "Confman-version": version,
                "Main-Class": "com.ninjamind.confman.ConfmanApplication",
                "Class-Path": configurations.runtime.files.collect { 'dependencies/' + it.name }.join(' ') + ' . '
        )
    }
    //We want a jar without any version in the name
    version = ''
    exclude '**/*.properties'
}

//Several files and directories have to be present in the delivery directory
task copyForDelivery << {
    //The dependencies must be present
    copy {
        into "$buildDir/libs/dependencies"
        from configurations.runtime
    }
    //... and the webapp too
    copy {
        into "$buildDir/libs/app"
        from "app"
    }
    //... and the properties
    copy {
        into "$buildDir/libs"
        from "$buildDir/resources/main"
        include '*.properties'
    }
    //Flyway copmmand line
    copy {
        into "$buildDir/libs/database"
        from "src/main/database"
    }
    //Database script
    copy {
        into "$buildDir/libs/database/sql"
        from "src/main/resources/db/migration/" + myDataBase
    }
    //Driver jdbc
    copy {
        into "$buildDir/libs/database/jars"
        if (myDataBase.equals("oracle")) {
            from configurations.runtime
            include '**/ojdbc6*.jar'
        } else {
            from configurations.runtime
            include '**/postgresql*.jar'
        }
    }
    //Create the flyway property file
    def props = new Properties()
    props.setProperty('flyway.user', project.property("db." + myDataBase + ".username"))
    props.setProperty('flyway.password', project.property("db." + myDataBase + ".password"))
    if (project.properties.containsKey("flyway.schemas") && !project.properties.containsKey("flyway.schemas").equals("")) {
        props.setProperty('flyway.schemas', project.property("db." + myDataBase + ".schemas"))
    }
    props.setProperty('flyway.url', project.property("db." + myDataBase + ".url"))
    new File("$buildDir/libs/database/conf/flyway.properties").withWriter {
        fileWriter -> props.each { key, value -> fileWriter.writeLine "$key=$value" }
    }
}

//We have to make the job before the jar completude
jar.dependsOn copyForDelivery
